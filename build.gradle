// Gradle build script for Maven artifacts and non-Android native libraries

plugins {
    id 'cpp'
    id 'java-library'
    id 'maven-publish'
}

ext {
    artifact = 'Libbulletjme'
    version = '9.2.4'
    baseName = "${artifact}-${version}"
}

sourceSets.main.java {
    srcDir 'src/main/java'
    srcDir 'src/main/native' // for IDE access (no Java there)
}

sourceSets.test.java {
    srcDir 'src/test/java'
}

// Regenerate all JNI header files before compiling any C++ source code.
tasks.withType(CppCompile) {
    dependsOn('classes')
}

String javaHome = org.gradle.internal.jvm.Jvm.current().javaHome.absolutePath
model {
    buildTypes {
        Debug
        Release
    }

    flavors {
        Sp // single-precision arithmetic
        Dp // double-precision arithmetic
    }

    platforms {
        Linux32 {
            architecture 'x86'
            operatingSystem 'linux'
        }
        Linux64 {
            architecture 'x86_64'
            operatingSystem 'linux'
        }
        Linux_ARM32 {
            architecture 'armel'
            operatingSystem 'linux'
        }
        Linux_ARM64 {
            architecture 'aarch64'
            operatingSystem 'linux'
        }
        MacOSX32 {
            architecture 'x86'
            operatingSystem 'osx'
        }
        MacOSX64 {
            architecture 'x86_64'
            operatingSystem 'osx'
        }
        Windows32 {
            architecture 'x86'
            operatingSystem 'windows'
        }
        Windows64 {
            architecture 'x86_64'
            operatingSystem 'windows'
        }
    }

    toolChains { // prioritize among the native toolchains
        visualCpp(VisualCpp)
        //clang(Clang)
        gcc(Gcc)
        clang(Clang)
        gcc5Arm32(Gcc) {
            target('Linux_ARM32') {
                cppCompiler.executable = 'arm-linux-gnueabi-g++-5'
                linker.executable = 'arm-linux-gnueabi-g++-5'
            }
        }
        gccArm64(Gcc) {
            target('Linux_ARM64') {
                cppCompiler.executable = 'aarch64-linux-gnu-g++'
                linker.executable = 'aarch64-linux-gnu-g++'
            }
        }
        gcc8Arm64(Gcc) {
            target('Linux_ARM64') {
                cppCompiler.executable = 'aarch64-linux-gnu-g++-8'
                linker.executable = 'aarch64-linux-gnu-gcc-8'
            }
        }
    }

    components {
        bulletjme(NativeLibrarySpec) {
            targetPlatform 'Linux32'
            targetPlatform 'Linux64'
            targetPlatform 'Linux_ARM32'
            targetPlatform 'Linux_ARM64'
            targetPlatform 'MacOSX32'
            targetPlatform 'MacOSX64'
            targetPlatform 'Windows32'
            targetPlatform 'Windows64'

            sources.cpp.source {
                srcDir 'src/main/native/bullet3'
                srcDir 'src/main/native/glue'
                srcDir 'src/main/native/v-hacd/src'
                include '**/*.cpp'
            }

            binaries.withType(StaticLibraryBinarySpec) {
                buildable = false
            }
            binaries.withType(SharedLibraryBinarySpec) {
                // Decide whether to build the target platform:
                if (gradle.rootProject.hasProperty('github')) {
                    // -Pgithub= specified on the command line
                    String github = gradle.rootProject.ext.github
                    if (github.equals('xcode11')) {
                        buildable = targetPlatform.name.equals('MacOSX64')
                    }
                } else if (gradle.rootProject.hasProperty('travis')) {
                    // -Ptravis= specified on the command line
                    String travis = gradle.rootProject.ext.travis
                    if (travis.equals('amd64')) {
                        buildable = !targetPlatform.name.startsWith('Linux_ARM')
                    } else if (travis.equals('arm32')) {
                        buildable = targetPlatform.name.startsWith('Linux_ARM32')
                    } else if (travis.equals('arm64')) {
                        buildable = targetPlatform.name.startsWith('Linux_ARM64')
                    } else if (travis.equals('osx')) {
                        buildable = targetPlatform.name.startsWith('MacOSX')
                    }
                } else { // neither -Pgithub= nor -Ptravis= specified
                    buildable = !targetPlatform.name.startsWith('Linux_ARM')
                    //buildable = targetPlatform.name.equals('Linux64')
                }

                if (buildType == buildTypes.Debug) {
                    cppCompiler.define '_DEBUG'

                    //cppCompiler.define 'BT_DEBUG_MEMORY_ALLOCATIONS'
                    //cppCompiler.define 'DEBUG_PERSISTENCY'
                }

                // Decide whether to build the current flavor:
                if (buildable && gradle.rootProject.hasProperty('flavor')) {
                    // -Pflavor= specified on the command line
                    String flavorArg = gradle.rootProject.ext.flavor
                    buildable = flavor.name.equals(flavorArg)
                }
                if (flavor == flavors.Dp) {
                    cppCompiler.define 'BT_USE_DOUBLE_PRECISION'
                    //buildable = false
                }

                cppCompiler.define 'BT_NO_PROFILE'

                String q = targetPlatform.name + buildType.name + flavor.name
                if (toolChain in VisualCpp) {
                    cppCompiler.define 'WIN32'
                    cppCompiler.args '/EHsc' // synchronous exceptions only
                    cppCompiler.args "/I$javaHome/include"
                    cppCompiler.args "/I$javaHome/include/win32"
                    cppCompiler.args "/I$projectDir/src/main/native/bullet3"
                    cppCompiler.args "/I$projectDir/src/main/native/bullet3/BulletDynamics/Featherstone"
                    cppCompiler.args "/I$projectDir/src/main/native/bullet3/LinearMath"
                    cppCompiler.args "/I$projectDir/src/main/native/v-hacd/inc"
                    cppCompiler.args "/I$projectDir/src/main/native/v-hacd/public"
                    if (buildType == buildTypes.Debug) {
                        cppCompiler.args '/FS' // serialize PDB writes
                        cppCompiler.args '/MTd' // use LIBCMTD
                        cppCompiler.args '/Zi' // produce PDB file
                        linker.args '/DEBUG'
                        if (buildable) {
                            String pdbFile = 'build/libs/bulletjme/shared/'
                            pdbFile += targetPlatform.name + '/debug/'
                            pdbFile += flavor.name + '/bulletjme.pdb'

                            task "copyPdbToDist$q" (type: Copy, dependsOn:
                                 "bulletjme${q}SharedLibrary") {
                                from pdbFile
                                rename { String filename ->
                                    return q + '_' + filename
                                }
                                into 'dist'
                            }
                            jar.dependsOn("copyPdbToDist$q")
                        }
                    }

                } else { // toolChain in Clang or Gcc
                    cppCompiler.args '-I', "$javaHome/include"
                    cppCompiler.args '-I', "$projectDir/src/main/native/bullet3"
                    cppCompiler.args '-I', "$projectDir/src/main/native/bullet3/BulletDynamics/Featherstone"
                    cppCompiler.args '-I', "$projectDir/src/main/native/bullet3/LinearMath"
                    cppCompiler.args '-I', "$projectDir/src/main/native/v-hacd/inc"
                    cppCompiler.args '-I', "$projectDir/src/main/native/v-hacd/public"
                    cppCompiler.args '-std=c++11'
                    if (buildType == buildTypes.Debug) {
                        cppCompiler.args '-O0', '-g3'
                    } else if (buildType == buildTypes.Release) {
                        cppCompiler.args '-O3'
                    }

                    String os = targetPlatform.operatingSystem.name
                    if (os == 'osx') {
                        cppCompiler.args '-I', "$javaHome/include/darwin"
                    } else if (os == 'linux') {
                        cppCompiler.args '-I', "$javaHome/include/linux"
                        cppCompiler.args '-fPIC'
                        cppCompiler.args '-fvisibility=hidden'
                        linker.args '-fvisibility=hidden'
                    } else if (os == 'windows') { // not tested recently
                        cppCompiler.define 'WIN32'
                        cppCompiler.args '-I', "$javaHome/include/win32"
                        cppCompiler.args '-static'
                        linker.args '-static'
                        linker.args '-Wl,--exclude-all-symbols'
                    } else {
                        buildable = false
                    }
                }

                if (buildable) {
                    println 'Build ' + q + ' using ' + toolChain

                    task "copyToDist$q" (type: Copy, dependsOn:
                         "bulletjme${q}SharedLibrary") {
                        from sharedLibraryFile
                        rename { String filename ->
                            return q + '_' + filename
                        }
                        into 'dist'
                    }
                    jar.dependsOn('copyToDist' + q)
                } else {
                    //println 'Do not build ' + q
                }
            }
        }
    }
}

description = 'JNI interface to Bullet Physics and V-HACD'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    testImplementation 'junit:junit:4.13.1'
}

repositories {
    //mavenLocal()
    jcenter()
}

tasks.withType(JavaCompile) { // compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.headerOutputDirectory = new File('src/main/native/glue')
}

test.dependsOn('assemble')
assemble.dependsOn('copyToDistJars')
task copyToDistJars (type: Copy) {
    dependsOn 'jar', 'javadocJar', 'sourcesJar'
    from 'build/libs'
    include '*.jar'
    into 'dist'
}

// custom tasks for creating source/javadoc JARs
jar {
    archiveBaseName = project.ext.baseName
}

task javadocJar(type: Jar) {
    archiveBaseName = project.ext.baseName
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveBaseName = project.ext.baseName
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of Java sourcecode.'
    from 'src/main/java'
}

build.dependsOn('pom')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into 'dist'
    rename '.*', baseName + '.pom'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId artifact
            from components.java
            groupId 'com.github.stephengold'
            pom {
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
            }
            version project.ext.version
        }
    }
}

// cleanup tasks

clean.dependsOn('cleanCxx', 'cleanDist', 'cleanLogs')
task cleanCxx(type: Delete) {
    delete '.cxx'
}
task cleanDist(type: Delete) {
    delete 'dist'
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
