// Gradle script to build Android native libraries

plugins {
    alias(libs.plugins.android.library)
}

// $artifact is set in the gradle.properties file
// or by a -Partifact= option on the command line.

String flavor = 'Sp'
if (project.hasProperty('flavor')) {
    flavor = project.ext.flavor
}

android {
    buildTypes {
        debug {
            jniDebuggable = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    compileSdk = 30
    defaultConfig {
        minSdk = 22
        ndk {
            if (!project.hasProperty('target')) {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            } else if (target == 'Android_ARM7') {
                abiFilters 'armeabi-v7a'
            } else if (target == 'Android_ARM8') {
                abiFilters 'arm64-v8a'
            } else if (target == 'Android_X86') {
                abiFilters 'x86'
            } else if (target == 'Android_X86_64') {
                abiFilters 'x86_64'
            }
        }
        targetSdk = 30
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    namespace = 'com.github.stephengold.libbulletjme'
    ndkVersion = '27.2.12479018'
}

dependencies {
    implementation(libs.jaxb.api)
}

// Register cleanup tasks:

clean.dependsOn('cleanCxx')

tasks.register('cleanCxx', Delete) {
    delete '.cxx'
}
