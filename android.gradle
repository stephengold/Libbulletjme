// Gradle script to build/publish Libbulletjme Android artifacts

plugins {
    id 'maven-publish' // to publish artifacts to Maven repositories
    id 'signing'       // to sign artifacts for publication

    alias(libs.plugins.android.library) // to build Android libraries
    alias(libs.plugins.validate.poms) // to verify POMs provide all info required by Maven Central
}

// $artifact is set in the gradle.properties file
// or by a -Partifact= option on the command line.

ext {
    groupID = 'com.github.stephengold'
    lbjVersion = '21.3.2'
    baseName = "${artifact}-${lbjVersion}" // for artifacts
    websiteUrl = 'https://github.com/stephengold/Libbulletjme'
}

android {
    buildTypes {
        debug {
            externalNativeBuild {
                ndkBuild {
                    cppFlags '-D_DEBUG'
                    //cppFlags '-DBT_ADDITIONAL_DEBUG'
                    //cppFlags '-DBT_DEBUG_MEMORY_ALLOCATIONS'
                    //cppFlags '-DDEBUG_PERSISTENCY'
                    //cppFlags '-DVERBOSE_RESIDUAL_PRINTF'
                }
            }
            jniDebuggable = true
        }
        release {
            minifyEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    compileSdk = 35
    defaultConfig {
        aarMetadata {
            minCompileSdk = 22
        }
        minSdk = 22
        ndk {
            if (!project.hasProperty('target')) {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            } else if (target == 'Android_ARM7') {
                abiFilters 'armeabi-v7a'
            } else if (target == 'Android_ARM8') {
                abiFilters 'arm64-v8a'
            } else if (target == 'Android_X86') {
                abiFilters 'x86'
            } else if (target == 'Android_X86_64') {
                abiFilters 'x86_64'
            }
        }
        targetSdk = 22
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    flavorDimensions 'precision'
    namespace = groupID + '.libbulletjme'
    ndkVersion = '27.2.12479018' // r27c
    productFlavors {
        if (!project.hasProperty('flavor') || flavor == 'Dp') {
            Dp { // flavor with double-precision locations
                dimension 'precision'
                externalNativeBuild {
                    ndkBuild {
                        cppFlags '-DBT_USE_DOUBLE_PRECISION'
                    }
                }
            }
        }
        if (!project.hasProperty('flavor') || flavor == 'Sp') {
            Sp { // flavor with single-precision locations
                dimension 'precision'
            }
        }
    }
    publishing {
        multipleVariants('complete') {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    testImplementation(libs.junit4)
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn 'publishMavenPublicationToMavenLocal'
    description = 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn 'publishMavenPublicationToOSSRHRepository'
    description = 'Stages Maven artifacts to Sonatype OSSRH.'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = artifact
            afterEvaluate { from components.complete }
            groupId = groupID
            pom {
                description = 'a JNI interface for Bullet Physics and V-HACD'
                developers {
                    developer {
                        email = 'sgold@sonic.net'
                        id = 'stephengold'
                        name = 'Stephen Gold'
                    }
                }
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                name = groupID + ':' + artifact
                scm {
                    connection = 'scm:git:git://github.com/stephengold/Libbulletjme.git'
                    developerConnection = 'scm:git:ssh://github.com:stephengold/Libbulletjme.git'
                    url = websiteUrl + '/tree/master'
                }
                url = websiteUrl
            }
            version = lbjVersion
        }
    }

    repositories {
        maven { // the staging repo of Sonatype OSSRH

            // Staging to OSSRH relies on the existence of 2 properties
            // (ossrhUsername and ossrhPassword)
            // which should be set in the ~/.gradle/gradle.properties file
            // or by -P options on the command line.

            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'Unknown user'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'Unknown password'
            }
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}
publishMavenPublicationToMavenLocal.dependsOn('assemble')
publishMavenPublicationToMavenLocal.doLast {
    println 'installed locally as ' + baseName
}

// Register tasks to sign artifacts for publication:

//   Signing relies on the existence of 2 properties
//   (signingKeyEncoded and signingPassword)
//   which should be set in the ~/.gradle/gradle.properties file
//   or by -P options on the command line.

signing {
    def signingKey = base64Decode(findProperty('signingKeyEncoded'))
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign configurations.archives
    sign publishing.publications.maven
}
tasks.withType(Sign).configureEach {
    onlyIf { project.hasProperty('signingKeyEncoded') }
}

static def base64Decode(encodedString) {
    if (encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    }
    return null
}
